#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий 

Процедура ПередЗаписью(Отказ)	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	um_ОбъектыМетаданных.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.um_ОбъектыМетаданных КАК um_ОбъектыМетаданных
	|ГДЕ
	|	um_ОбъектыМетаданных.Наименование = &Наименование
	|	И um_ОбъектыМетаданных.Родитель = &Родитель
	|	И НЕ um_ОбъектыМетаданных.Ссылка = &Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Уже существуют элементы с наименованием %1 в ветке %2.'; en = 'There are elements with description %1 at group %2'"), 
			Наименование,
			Родитель);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Наименование", "Объект", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли