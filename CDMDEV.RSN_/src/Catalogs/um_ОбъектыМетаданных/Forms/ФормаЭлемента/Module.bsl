#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрокаВТаблицуФормы("ИменаТабличныхЧастей");
	СтрокаВТаблицуФормы("ИменаРеквизитовТипаДата");
	
	СтрокаJsonВТаблицуФормы("СсылочныеТипы");
	СтрокаJsonВТаблицуФормы("ПоляПоискаРеквизитов"); 
	
	Элементы.ПояснениеКДопОбработчикам.Заголовок =
	"Для инициации доп. обработчиков, в теле запроса к сервису необходимо передать структуру ""ДопОбработчики[ВидОбрабочика]""
	|В дополнительных обработчиках используются следующие переменные: 
	|- ""ОбрабатываемыйОбъект"" - обрабатываемый элемент объекта метаданных;
	|- ""ВидОбработчика"" - строка - значения: ""ПередЗаписью"" или ""ПриЗаписи"";
	|- ""ПараметрыОбъекта"" - структура - ""техническая"" информация по объекту из справочника ""um_ОбъектыМетаданных"";
	|- ""РеквизитыЗапроса"" - структура - реквизиты, переданные в теле запроса к сервису";
	
КонецПроцедуры    

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТаблицаФормыВСтроку("ИменаТабличныхЧастей", ТекущийОбъект);
	ТаблицаФормыВСтроку("ИменаРеквизитовТипаДата", ТекущийОбъект);
	
	ТаблицаФормыВJson("СсылочныеТипы", ТекущийОбъект);        
	ТаблицаФормыВJson("ПоляПоискаРеквизитов", ТекущийОбъект);        
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура СтрокаВТаблицуФормы(ИмяРеквизита)
	
	Если Не Объект[ИмяРеквизита] = "" Тогда
		
		Значения = СтрРазделить(Объект[ИмяРеквизита], ",",Ложь);
		Для Каждого Стр Из Значения Цикл
			НовСтр = ЭтотОбъект[ИмяРеквизита].Добавить();
			НовСтр.Имя = Стр; 
		КонецЦикла;	
		
	КонецЕсли;	

КонецПроцедуры  

&НаСервере
Процедура СтрокаJsonВТаблицуФормы(ИмяРеквизита)
	            	
	Если Не Объект[ИмяРеквизита] = "" Тогда     
		
		Значения = um_httpСервис.JSONПрочитать(Объект[ИмяРеквизита]); 	
		Для Каждого Стр Из Значения Цикл
			НовСтр = ЭтотОбъект[ИмяРеквизита].Добавить();
			НовСтр.ИмяРеквизита = Стр.Ключ; 
			НовСтр.Тип = Стр.Значение; 
		КонецЦикла;	
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ТаблицаФормыВСтроку(ИмяРеквизита, ТекущийОбъект)
	
	Значения = Новый Массив;
	Для Каждого Стр Из ЭтотОбъект[ИмяРеквизита] Цикл  
		Если Не ЗначениеЗаполнено(Стр.Имя) Тогда
			Продолжить;
		КонецЕсли;
		Значения.Добавить(СокрЛП(Стр.Имя));	
	КонецЦикла;
	ТекущийОбъект[ИмяРеквизита] = СтрСоединить(Значения, ",");	

КонецПроцедуры

&НаСервере
Процедура ТаблицаФормыВJson(ИмяРеквизита, ТекущийОбъект)
	
	Значения = Новый Структура;
	Для Каждого Стр Из ЭтотОбъект[ИмяРеквизита] Цикл  
		Если Не ЗначениеЗаполнено(Стр.ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		Значения.Вставить(СокрЛП(Стр.ИмяРеквизита), СокрЛП(Стр.Тип));	
	КонецЦикла;   
	ТекущийОбъект[ИмяРеквизита] = um_httpСервис.JSONЗаписать(Значения);

КонецПроцедуры

 #КонецОбласти
