#Область ОбработчикиСобытийФормы 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ДлительнаяОперация = ЗаполнитьНаСервере(); 
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	
	ОповещениеПользователя = ПараметрыОжидания.ОповещениеПользователя;
	ОповещениеПользователя.Показать = Истина;
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,,ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыборочно(Команда)  
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнение() Тогда   
		
		ЗаполнитьВыборочноНаСервере();
		
		ТекстСообщения = НСтр("ru='Готово.';en='Done.'");   	
		ПоказатьПредупреждение(,ТекстСообщения);    
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы    

&НаКлиенте
Процедура ОбъектМетаданныхНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;       
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Ложь);
	
	ОбработкаВыбора = Новый ОписаниеОповещения("ПриЗакрытииФормыВыбораОбъектаМетаданных", ЭтаФорма);
	
	ОткрытьФорму("Справочник.ИдентификаторыОбъектовМетаданных.ФормаСписка",ПараметрыФормы,
				ЭтаФорма,ЭтаФорма.УникальныйИдентификатор,,,
					ОбработкаВыбора,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры  

#КонецОбласти

#Область СлужебныеПроцедурыИФункции   

&НаКлиенте
Процедура ПриЗакрытииФормыВыбораОбъектаМетаданных(Значение, ДопПараметры) Экспорт
	
	Если Значение = Неопределено Тогда
        Возврат;
    КонецЕсли;
	
	ОчиститьСообщения();
	ПриЗакрытииФормыВыбораОбъектаМетаданныхНаСервере(Значение);      
	
КонецПроцедуры 

&НаСервере
Процедура ПриЗакрытииФормыВыбораОбъектаМетаданныхНаСервере(Значение) 
	
	ОбъектМетаданныхПолноеИмя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение, "ПолноеИмя");      
	
	Обработки.um_ЗаполениеСведенийОбОбъектахМетаданных.ИмяОбъектаМетаданныхДляСервиса(Метаданные.НайтиПоПолномуИмени(ОбъектМетаданныхПолноеИмя), ОбъектМетаданныхВСервисе);
	
	ВидМетаданныхВСервисе = um_httpСервис.ВидМетаданныхПоИмениОбъекта(ОбъектМетаданныхПолноеИмя); 
	Если ВидМетаданныхВСервисе = "" Тогда 
		
		ОписаниеОшибки = НСтр("ru='Не удалось определить вид объекта метаданных в сервисе по имени: %1.'
							|;en='The metadata object type could not be identified by name: %1.'");	
		
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ОписаниеОшибки, ОбъектМетаданныхПолноеИмя),,"ИмяВидаМетаданныхВСервисе");		
		
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Функция ЗаполнитьНаСервере()   
	
	ПараметрыЗаполнения = Новый Структура("ТолькоВерхнийУровень, УстанавливатьПризнакНеИспользовать", ТолькоВерхнийУровень, УстанавливатьПризнакНеИспользовать);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(,"Обработки.um_ЗаполениеСведенийОбОбъектахМетаданных.ЗаполнитьСправочникОбъектовМетаданных", ПараметрыЗаполнения); 
	             
КонецФункции

&НаСервере
Процедура ЗаполнитьВыборочноНаСервере()
	
	ПараметрыЗаполнения = um_httpСервис.ПараметрыЗаполненияОбъектаМетаданных();
	ПараметрыЗаполнения.ПолноеИмяОбъектаМетаданных = ОбъектМетаданныхПолноеИмя;
	ПараметрыЗаполнения.ИмяВидаОбъектовВВебСервисе = ВидМетаданныхВСервисе;
	ПараметрыЗаполнения.ИмяОбъектаВВебСервисе = ОбъектМетаданныхВСервисе;
	ПараметрыЗаполнения.ТекстЗапроса = ТекстЗапроса;    
	
	Обработки.um_ЗаполениеСведенийОбОбъектахМетаданных.ЗаполнитьЭлементСправочникаОбъектовМетаданных(ПараметрыЗаполнения);

КонецПроцедуры 

#КонецОбласти
