#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда    
	
#Область ПрограммныйИнтерфейс   

Процедура ОбновитьСписокИнформационныхБаз() Экспорт     
	
	БазыСерверов = Новый ТаблицаЗначений; 
	БазыСерверов.Колонки.Добавить("ИмяБазы", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));    
	БазыСерверов.Колонки.Добавить("СерверПриложений", Новый ОписаниеТипов("СправочникСсылка.СерверПриложений"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерверПриложений.Ссылка КАК СерверПриложений,
		|	СерверПриложений.Наименование КАК СерверПриложенийНаименование
		|ИЗ
		|	Справочник.СерверПриложений КАК СерверПриложений
		|ГДЕ
		|	НЕ СерверПриложений.ПометкаУдаления
		|	И СерверПриложений.МониторингБаз";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		БазыСервера = ПолучитьСписокБазСервера(Выборка.СерверПриложенийНаименование);	
		Для Каждого База Из БазыСервера Цикл 	
			ЗаполнитьЗначенияСвойств(БазыСерверов.Добавить(), Новый Структура("ИмяБазы, СерверПриложений", База, Выборка.СерверПриложений));				
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазыСерверов.ИмяБазы КАК ИмяБазы,
		|	БазыСерверов.СерверПриложений КАК СерверПриложений
		|ПОМЕСТИТЬ ВТ_БазыСерверов
		|ИЗ
		|	&БазыСерверов КАК БазыСерверов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_БазыСерверов.ИмяБазы КАК ИмяБазы,
		|	ВТ_БазыСерверов.СерверПриложений КАК СерверПриложений,
		|	ВТ_БазыСерверов.СерверПриложений.Контур КАК Контур,
		|	ВТ_БазыСерверов.СерверПриложений.Среда КАК Среда,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТ_БазыСерверов.СерверПриложений) КАК СерверПриложенийПредставление,
		|	ИнформационныеБазы.Ссылка КАК ИнформационнаяБаза
		|ИЗ
		|	ВТ_БазыСерверов КАК ВТ_БазыСерверов
		|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ИнформационныеБазы КАК ИнформационныеБазы
		|		ПО ВТ_БазыСерверов.ИмяБазы = ИнформационныеБазы.Наименование
		|			И ВТ_БазыСерверов.СерверПриложений = ИнформационныеБазы.СерверПриложений
		|ГДЕ
		|	(ВТ_БазыСерверов.ИмяБазы ЕСТЬ NULL
		|				И НЕ ИнформационныеБазы.ПометкаУдаления
		|			ИЛИ ИнформационныеБазы.Ссылка ЕСТЬ NULL)";
	
	Запрос.УстановитьПараметр("БазыСерверов", БазыСерверов);    
	
	Выборка = Запрос.Выполнить().Выбрать();  
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ИмяБазы) Тогда
			ДобавитьБазу(Выборка.ИмяБазы, Выборка.СерверПриложений, Выборка.Контур, Выборка.Среда, Выборка.СерверПриложенийПредставление, Ложь);	
		ИначеЕсли ЗначениеЗаполнено(Выборка.ИнформационнаяБаза) Тогда
			ИнформационнаяБаза = Выборка.ИнформационнаяБаза.ПолучитьОбъект();
			ИнформационнаяБаза.УстановитьПометкуУдаления(Истина);
		КонецЕсли;                                                                                              		
				
	КонецЦикла;
	
КонецПроцедуры  

Функция ПолучитьСписокБазСервера(ИмяСервераПриложений) Экспорт
	
	Результат = Новый Массив; 
	
	Соединитель = Новый COMObject("V83.COMConnector"); 
	
	Порт = "1540"; 
	СтрокаСоединения = "TCP://" + ИмяСервераПриложений + ":" + Порт;
	
	СоединениеСАгентом = Соединитель.ConnectAgent(СтрокаСоединения);
	МассивКластеров = СоединениеСАгентом.GetClusters();
	
	// получается первый массив кластера.
	ОписаниеКластера = МассивКластеров.GetValue(МассивКластеров.GetLowerBound());
	
	// в случае если определены администраторы кластера, должны быть указаны данные о логине.
	СоединениеСАгентом.authenticate(ОписаниеКластера, "", "");    
	
	МассивБаз = СоединениеСАгентом.GetInfoBases(ОписаниеКластера);
	МаксимальныйИндекс = МассивБаз.GetUpperBound();
	
	Для ТекущийИндекс = МассивБаз.GetLowerBound() По МаксимальныйИндекс Цикл
		ОписаниеБазы = МассивБаз.GetValue(ТекущийИндекс); 
				
		Результат.Добавить(ОписаниеБазы.Name);		
			
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьБазыВСписок(СерверПриложений, ИменаБаз) Экспорт
	
	Если Не ЗначениеЗаполнено(СерверПриложений) ИЛИ ИменаБаз.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИменБаз = Новый ТаблицаЗначений; 
	ТаблицаИменБаз.Колонки.Добавить("ИмяБазы", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));    
	ТаблицаИменБаз.Колонки.Добавить("СерверПриложений", Новый ОписаниеТипов("СправочникСсылка.СерверПриложений"));
	
	Для Каждого Стр Из ИменаБаз Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаИменБаз.Добавить(), Новый Структура("ИмяБазы, СерверПриложений", Стр, СерверПриложений));	
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИменаБаз.ИмяБазы КАК ИмяБазы,
		|	ИменаБаз.СерверПриложений КАК СерверПриложений
		|ПОМЕСТИТЬ ВТ_ИменаБаз
		|ИЗ
		|	&ТаблицаИменБаз КАК ИменаБаз
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ИменаБаз.ИмяБазы КАК ИмяБазы,
		|	ВТ_ИменаБаз.СерверПриложений КАК СерверПриложений, 
		|	ВТ_ИменаБаз.СерверПриложений.Контур КАК Контур,
		|	ВТ_ИменаБаз.СерверПриложений.Среда КАК Среда,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТ_ИменаБаз.СерверПриложений) КАК СерверПриложенийПредставление
		|ИЗ
		|	ВТ_ИменаБаз КАК ВТ_ИменаБаз
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИнформационныеБазы КАК ИнформационныеБазы
		|		ПО ВТ_ИменаБаз.ИмяБазы = ИнформационныеБазы.Наименование
		|			И ВТ_ИменаБаз.СерверПриложений = ИнформационныеБазы.СерверПриложений
		|ГДЕ
		|	ИнформационныеБазы.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТаблицаИменБаз", ТаблицаИменБаз);
		
	Выборка = Запрос.Выполнить().Выбрать();  
	
	Пока Выборка.Следующий() Цикл  
		ДобавитьБазу(Выборка.ИмяБазы, Выборка.СерверПриложений, Выборка.Контур, Выборка.Среда, Выборка.СерверПриложенийПредставление);		
	КонецЦикла; 		

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Функция ДобавитьБазу(Наименование, СерверПриложений, Контур, Среда, СерверПриложенийПредставление, УказыватьПутьНаВебСервере = Истина)
	          
	НоваяБаза = Справочники.ИнформационныеБазы.СоздатьЭлемент();
	НоваяБаза.Наименование = Наименование;   
	НоваяБаза.СерверПриложений = СерверПриложений; 
	НоваяБаза.Контур = Контур;
	НоваяБаза.Среда = Среда;
	
	Если ВРег(Лев(Наименование,6)) = "ACC_BX" Тогда
		НоваяБаза.Конфигурация = Перечисления.КонфигурацииИнформационныхБаз.БухгалтерияБИТ;
	ИначеЕсли ВРег(Лев(Наименование,3)) = "ACC" Тогда
		НоваяБаза.Конфигурация = Перечисления.КонфигурацииИнформационныхБаз.Бухгалтерия;
	ИначеЕсли ВРег(Лев(Наименование,3)) = "HRM" Тогда                                                         
		НоваяБаза.Конфигурация = Перечисления.КонфигурацииИнформационныхБаз.Зарплата;
	ИначеЕсли ВРег(Лев(Наименование,6)) = "DOCMNG" Тогда                                                         
		НоваяБаза.Конфигурация = Перечисления.КонфигурацииИнформационныхБаз.Документооборот;
	ИначеЕсли ВРег(Лев(Наименование,2)) = "UH" Тогда                                                         
		НоваяБаза.Конфигурация = Перечисления.КонфигурацииИнформационныхБаз.УправлениеХолдингом;
	КонецЕсли;
	
	НоваяБаза.Тестовая = (Не Среда = Перечисления.Среда.Prod);
	
	Если УказыватьПутьНаВебСервере Тогда
		НоваяБаза.ПутьНаВебСервере = СерверПриложенийПредставление + "/" + Наименование;
	КонецЕсли;
		
	НоваяБаза.Записать();
	
	Возврат НоваяБаза.Ссылка; 	
	
КонецФункции

#КонецОбласти

#КонецЕсли