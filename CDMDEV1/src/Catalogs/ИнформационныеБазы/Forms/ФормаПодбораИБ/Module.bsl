#Область ОбработчикиСобытийФормы
  
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Кластеры = ИБ.ПолучитьЭлементы();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнформационныеБазы.Ссылка КАК Ссылка,
		|	ИнформационныеБазы.СерверПриложений КАК СерверПриложений
		|ИЗ
		|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
		|
		|УПОРЯДОЧИТЬ ПО
		|	СерверПриложений,
		|	ИнформационныеБазы.Наименование
		|ИТОГИ ПО
		|	СерверПриложений";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаКластеры = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
	Пока ВыборкаКластеры.Следующий() Цикл 
		
		НовыйКластер = Кластеры.Добавить();
		НовыйКластер.Подобрать = Ложь;
		НовыйКластер.База = ВыборкаКластеры.СерверПриложений;   
		
		Базы = НовыйКластер.ПолучитьЭлементы();
		
		Выборка = ВыборкаКластеры.Выбрать();
		Пока Выборка.Следующий() Цикл 
			
			База = Базы.Добавить();	
			База.Подобрать = Ложь;
			База.База = Выборка.Ссылка;
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Результат = Новый Массив;
	
	Кластеры = ИБ.ПолучитьЭлементы();
	Для Каждого Кластер ИЗ Кластеры Цикл
		Базы = Кластер.ПолучитьЭлементы();
		Для Каждого Стр Из Базы Цикл
			Если Стр.Подобрать	Тогда
				Результат.Добавить(Стр.База);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	ОбработатьФлажки(Истина);
КонецПроцедуры 

&НаКлиенте
Процедура СнятьФлажки(Команда)
	ОбработатьФлажки(Ложь);
КонецПроцедуры
             
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ИБПодобратьПриИзменении(Элемент)
	
	ТекДанные = Элементы.ИБ.ТекущиеДанные;  
	Базы = ТекДанные.ПолучитьЭлементы();
	Для Каждого Стр Из Базы Цикл
		Стр.Подобрать = ТекДанные.Подобрать;	
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьФлажки(Установить)
	
	Кластеры = ИБ.ПолучитьЭлементы();
	Для Каждого Кластер ИЗ Кластеры Цикл
		Кластер.Подобрать = Установить;
		Базы = Кластер.ПолучитьЭлементы();
		Для Каждого База Из Базы Цикл
			База.Подобрать = Установить;	
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры 


#КонецОбласти
