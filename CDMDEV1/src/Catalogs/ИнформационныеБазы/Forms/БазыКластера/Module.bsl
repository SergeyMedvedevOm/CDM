			   
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)   
	
	Если Параметры.Свойство("СерверПриложений") И ЗначениеЗаполнено(Параметры.СерверПриложений) Тогда
		СерверПриложений = Параметры.СерверПриложений; 
		ОбновитьСписокБазКластераНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти    

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СерверПриложенийПриИзменении(Элемент)
	ОбновитьСписокБазКластераНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписокБазКластера(Команда) 
	
	ОбновитьСписокБазКластераНаСервере();
	
КонецПроцедуры  

&НаКлиенте
Процедура ПеренестиВСписок(Команда)  
	
	ИменаБаз = Новый Массив;
	Для Каждого Стр Из СписокБаз Цикл 
		Если Стр.Добавить Тогда
			ИменаБаз.Добавить(Стр.ИмяБазы);	
		КонецЕсли;
	КонецЦикла;
	
	Если ИменаБаз.Количество() = 0 Тогда
		Закрыть();  	
	Иначе
		Результат = Новый Структура;
		Результат.Вставить("СерверПриложений", СерверПриложений);
		Результат.Вставить("ИменаБаз", ИменаБаз);
	
		Закрыть(Результат);		
	КонецЕсли;      
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокБазКластераНаСервере() 
	
	Если Не ЗначениеЗаполнено(СерверПриложений) Тогда
		Возврат;
	КонецЕсли;
	
	СписокБаз.Очистить();
	
	ИмяСервераПриложений = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СерверПриложений, "Наименование");
	БазыСервера = Справочники.ИнформационныеБазы.ПолучитьСписокБазСервера(ИмяСервераПриложений);
	
	Для Каждого База Из БазыСервера Цикл 	
		ЗаполнитьЗначенияСвойств(СписокБаз.Добавить(), Новый Структура("ИмяБазы", База));				
	КонецЦикла;   
	
	СписокБаз.Сортировать("ИмяБазы");
	
КонецПроцедуры 

#КонецОбласти
