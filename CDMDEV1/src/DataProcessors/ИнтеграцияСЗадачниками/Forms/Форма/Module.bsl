#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ОбновитьИнформацию(Команда) 
	
	ОповещениеОбработчикРезультата = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект); 
	
	ДлительнаяОперация = НачатьОбработкуНаСервере(); 
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);   
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОбработчикРезультата, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьОбработкуНаСервере()  
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
		
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыФоновогоЗадания, 
		"ИнтеграцияЗадачников.ОбновитьИнформациюПоЗадачам", Задачник, ДатаОбновления);
	
КонецФункции 

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли; 
	
	ПоказатьПредупреждение(, "Обновление информации из задачников завершено.");  
	
КонецПроцедуры 

#КонецОбласти
