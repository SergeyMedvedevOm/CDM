#Область СлужебныйПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

// Получает имя пользователя и пароль из ИБ. Все параметры -
// неявно возвращаемые значения.
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО.
//   ИмяКомпьютера - Строка - имя компьютера, хранящего часть пароля, или:
//                 - Неопределено - признак того, что пароль восстанавливать не нужно.
//   ПарольСохранен - Булево - Истина, если пароль сохранен.
//   ЧастьПароляВИБ - Строка - часть пароля, хранимая в ИБ.
//   ВременныйФайлЧастиПароля - Строка - путь ко временному файлу с другой частью пароля.
//   ИспользуетсяАутентификацияОС - Булево - Истина, если используется аутентификация операционной системы.
//
Процедура ПрочитатьНастройкиАвторизации(ИмяПользователя = "", Знач ИмяКомпьютера = Неопределено,
		ПарольСохранен = Ложь, ЧастьПароляВИБ = "",
		ВременныйФайлЧастиПароля = "", ИспользуетсяАутентификацияОС = Ложь) Экспорт
	
	ИмяПользователя = Неопределено;
	ПарольСохранен = Ложь;
	ИспользуетсяАутентификацияОС = Ложь;
		
	// Выберем имя пользователя и данные для восстановления пароля, или хотя бы имя пользователя. Приоритет -
	// у данных, сохраненных с текущего компьютера, чтобы обеспечить восстановление раздельно хранимого пароля.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиАвторизации.ИмяПользователя КАК ИмяПользователя,
		|	НастройкиАвторизации.ЧастьПароляВИнформационнойБазе КАК ЧастьПароляВИнформационнойБазе,
		|	НастройкиАвторизации.ВременныйФайлЧастиПароля КАК ВременныйФайлЧастиПароля,
		|	НастройкиАвторизации.ИспользуетсяАутентификацияОС КАК ИспользуетсяАутентификацияОС,
		|	ВЫБОР
		|		КОГДА НастройкиАвторизации.ХешИмениКомпьютера = &ХешИмениКомпьютера
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.НастройкиАвторизации КАК НастройкиАвторизации
		|ГДЕ
		|	НастройкиАвторизации.Пользователь = &Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет");
	Если ИмяКомпьютера = Неопределено Тогда // веб-клиент
		Запрос.УстановитьПараметр("ХешИмениКомпьютера", 0);
	Иначе // восстановим данные для пароля, если компьютер тот же, или хотя бы имя
		Запрос.УстановитьПараметр("ХешИмениКомпьютера", ХешСуммаCRC32(ИмяКомпьютера));
	КонецЕсли;
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ИмяПользователя = Выборка.ИмяПользователя;
		ПарольСохранен = ЗначениеЗаполнено(Выборка.ВременныйФайлЧастиПароля);
		ЧастьПароляВИБ = Выборка.ЧастьПароляВИнформационнойБазе;
		ВременныйФайлЧастиПароля = Выборка.ВременныйФайлЧастиПароля;
		ИспользуетсяАутентификацияОС = Выборка.ИспользуетсяАутентификацияОС; 	
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет имя пользователя, если необходимо, пароль.
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО.
//   Пароль - Строка - пароль пользователя ДО.
//   ИмяКомпьютера - Строка - имя компьютера, где хранится часть пароля, или:
//                 - Неопределено - признак того, что пароль сохранять не нужно.
//   ЧастьПароляВИБ - Строка - пароль целиком или его часть, хранимая в ИБ.
//   ВременныйФайлЧастиПароля - Строка - путь ко временному файлу с другой частью пароля.
//   ИспользуетсяАутентификацияОС - Булево - Истина, если используется аутентификация операционной системы.
//
Процедура СохранитьНастройкиАвторизации(Знач ИмяПользователя, Знач Пароль,
		Знач ИмяКомпьютера = Неопределено, Знач ЧастьПароляВИБ = "",
		Знач ВременныйФайлЧастиПароля = "", Знач ИспользуетсяАутентификацияОС = Ложь) Экспорт
	
	Если ИмяКомпьютера = Неопределено Тогда // веб-клиент
		ПарольСохранен = Ложь;
		ХешИмениКомпьютера = 0;
	Иначе
		ПарольСохранен = Истина;
		ХешИмениКомпьютера = ХешСуммаCRC32(ИмяКомпьютера);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиАвторизации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователи.ТекущийПользователь());
	НаборЗаписей.Отбор.ПарольСохранен.Установить(ПарольСохранен);
	НаборЗаписей.Отбор.ХешИмениКомпьютера.Установить(ХешИмениКомпьютера);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Пользователь = Пользователи.ТекущийПользователь();
	Запись.ПарольСохранен = ПарольСохранен;
	Запись.ХешИмениКомпьютера = ХешИмениКомпьютера;
	
	Запись.ИмяПользователя = ИмяПользователя;
	Запись.ЧастьПароляВИнформационнойБазе = ЧастьПароляВИБ; // не имеет смысла для веб-клиента
	Запись.ВременныйФайлЧастиПароля = ВременныйФайлЧастиПароля; // не имеет смысла для веб-клиента
	Запись.ИспользуетсяАутентификацияОС = ИспользуетсяАутентификацияОС;
		
	НаборЗаписей.Записать();   
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает хеш-сумму данных по алгоритму CRC32.
//
// Параметры:
//   Данные - Строка, ДвоичныеДанные - данные для расчета.
//
// Возвращаемое значение:
//   Число - хеш-сумма, рассчитанная по алгоритму CRC32.
//
Функция ХешСуммаCRC32(Данные)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
	ХешированиеДанных.Добавить(Данные);
	
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции             

#КонецОбласти
