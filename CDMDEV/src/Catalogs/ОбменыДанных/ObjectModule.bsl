#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий 

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
	
	Если Не ВыполнятьПоРасписанию И Ссылка.ВыполнятьПоРасписанию Тогда  
		УдалитьЗадание(); 		
	КонецЕсли;   
	
	Если Не ВыполнятьПоРасписанию Тогда
		Возврат
	КонецЕсли;
	
	// Создание регламентного задания - пустышки (для хранения его идентификатора в данных).
	РегламентныеЗаданияСервер.ЗаблокироватьРегламентноеЗадание(РегламентноеЗадание);

	Задание = РегламентныеЗаданияСервер.Задание(РегламентноеЗадание);
	Если Задание = Неопределено Тогда
		
		ПараметрыЗадания = Новый Структура();
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускОбработчиковДанных);
		ПараметрыЗадания.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.ЗапускОбработчиковДанных.ИмяМетода);
		ПараметрыЗадания.Вставить("Использование", Ложь);
		ПараметрыЗадания.Вставить("Наименование", ПредставлениеЗадания(Наименование));
 		Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		
		РегламентноеЗадание = РегламентныеЗаданияСервер.УникальныйИдентификатор(Задание);
		РегламентныеЗаданияСервер.ЗаблокироватьРегламентноеЗадание(РегламентноеЗадание);
	КонецЕсли;   
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если Не ВыполнятьПоРасписанию Тогда
		Возврат;	
	КонецЕсли;
	
	УдалитьЗадание();

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	РегламентноеЗадание = Неопределено;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если Не ВыполнятьПоРасписанию Тогда
		Возврат;	
	КонецЕсли;
	
	Задание = РегламентныеЗаданияСервер.Задание(РегламентноеЗадание);	
	Если Задание <> Неопределено Тогда
		Изменения = Новый Структура;
		
		ВключитьЗадание = ВыполнятьПоРасписанию И Не ПометкаУдаления;
		Если Задание.Использование <> ВключитьЗадание Тогда
			Изменения.Вставить("Использование", ВключитьЗадание);
		КонецЕсли;
		
		// Расписание устанавливается в форме элемента.
		Если ДополнительныеСвойства.Свойство("Расписание") 
			И ТипЗнч(ДополнительныеСвойства.Расписание) = Тип("РасписаниеРегламентногоЗадания")
			И Строка(ДополнительныеСвойства.Расписание) <> Строка(Задание.Расписание) Тогда
			Изменения.Вставить("Расписание", ДополнительныеСвойства.Расписание);
		КонецЕсли;		
		
		Если ТипЗнч(Задание) = Тип("РегламентноеЗадание") Тогда
			НаименованиеЗадания = ПредставлениеЗадания(Наименование);
			Если Задание.Наименование <> НаименованиеЗадания Тогда
				Изменения.Вставить("Наименование", НаименованиеЗадания);
			КонецЕсли;
		КонецЕсли;
		
		Если Задание.Параметры.Количество() <> 1 ИЛИ Задание.Параметры[0] <> Ссылка Тогда
			
			ПараметрыЗадания = Новый Массив;
			ПараметрыЗадания.Добавить(Ссылка);  
			
			МассивПолучателей = Новый Массив;
			ДопПараметры = Новый Структура; 
			Для Каждого Получатель Из АдресаРассылок Цикл
				МассивПолучателей.Добавить(Получатель.ЭлектронныйАдрес);		 
			КонецЦикла;
			ДопПараметры.Вставить("Получатели", СтрСоединить(МассивПолучателей, ";"));
			ДопПараметры.Вставить("ПараметрыОбработки", ""); 
			
			ПараметрыЗадания.Добавить(ДопПараметры); 
			
			Изменения.Вставить("Параметры", ПараметрыЗадания);
		КонецЕсли;
			
		Если Изменения.Количество() > 0 Тогда
			РегламентныеЗаданияСервер.ИзменитьЗадание(РегламентноеЗадание, Изменения);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры
     
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеЗадания(НаименованиеРассылки)
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Обработчик данных: %1'"), Наименование);
КонецФункции  

Процедура УдалитьЗадание()
	
	Если ЗначениеЗаполнено(РегламентноеЗадание) Тогда
		РегламентныеЗаданияСервер.УдалитьЗадание(РегламентноеЗадание);  	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли