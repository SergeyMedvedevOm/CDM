#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоНовый = Объект.Ссылка.Пустая(); 
	
	ОбменДанныхОснование = Параметры.ЗначениеКопирования;
	СозданКопированием = Не ОбменДанныхОснование.Пустая();
	
	Если ЭтоНовый И Не СозданКопированием Тогда
		ЗаполнитьРасписаниеПоУмолчанию();
	Иначе
		ПрочитатьРасписаниеРегламентногоЗадания();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Расписание", Расписание);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы	 

&НаКлиенте
Процедура ВыполнятьПоРасписаниюПриИзменении(Элемент)
	Если Объект.ВыполнятьПоРасписанию Тогда
		ИзменитьРасписаниеВДиалоге();
	КонецЕсли;
КонецПроцедуры  

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Расписание(Команда) 
	
	ИзменитьРасписаниеВДиалоге();
		
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьРасписаниеВДиалоге()
	Обработчик = Новый ОписаниеОповещения("ПослеИзмененияРасписания", ЭтотОбъект);
	ДиалогРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ДиалогРасписания.Показать(Обработчик);
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияРасписания(РасписаниеРезультат, ДополнительныеПараметры) Экспорт
	Если РасписаниеРезультат <> Неопределено Тогда
		Модифицированность = Истина;
		Расписание = РасписаниеРезультат;		
	КонецЕсли;
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьРасписаниеПоУмолчанию()
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ВремяНачала = '00010101073000'; // в 7:30 утра
	Расписание.ПериодПовтораДней = 1; // каждый день

КонецПроцедуры   

&НаСервере
Процедура ПрочитатьРасписаниеРегламентногоЗадания()
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторЗадания = ?(СозданКопированием, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбменДанныхОснование,
		"РегламентноеЗадание"), Объект.РегламентноеЗадание);
	Если ТипЗнч(ИдентификаторЗадания) = Тип("УникальныйИдентификатор") Тогда
		Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторЗадания);
		Если Задание <> Неопределено Тогда
			Расписание = Задание.Расписание;
		Иначе
			ЗаполнитьРасписаниеПоУмолчанию(); 
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры



#КонецОбласти 	
	