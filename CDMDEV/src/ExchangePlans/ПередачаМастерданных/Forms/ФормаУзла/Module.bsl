
#Область ОбработчикиСобытийЭлементовШапкиФормы    

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Объект.Код = Объект.Наименование;
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиОповещенийНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ПолучателиОповещенийЗавершениеВвода", ЭтотОбъект);
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(Оповещение, Объект.ПолучателиОповещений, "Укажите получателей оповещений через точку с запятой");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ЗаполнитьЗначениеОрганизациейИБ(Команда)   
	
	Если Не ЗначениеЗаполнено(Объект.ИнформационнаяБаза) Тогда  
		ТекстСообщения = НСтр("ru = 'Не выбрана информационная база.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.ИнформационнаяБаза");
		Возврат;
	КонецЕсли;
	
	ИДОрганизации = ПолучитьГУИДОрганизации(Объект.ИнформационнаяБаза); 
	Если Не ЗначениеЗаполнено(ИДОрганизации) Тогда
		ТекстСообщения = НСтр("ru = 'Не указана организация информационной базы.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.ИнформационнаяБаза");
		Возврат;	
	КонецЕсли;
	
	ТекДанные = Элементы.ПравилаРегистрации.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекДанные.Значение = ИДОрганизации; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьГУИДОрганизации(ИнформационнаяБаза)
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнформационнаяБаза, "Организация");
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Организация.УникальныйИдентификатор(); 
	
КонецФункции

&НаКлиенте
Процедура ПолучателиОповещенийЗавершениеВвода(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
	
	Если ВведенныйТекст = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Объект.ПолучателиОповещений = СтрЗаменить(ВведенныйТекст, " ", "");
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти