#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Дополняет записи по обработчику всеми продуктивными базами  
//
// Параметры:
//  Обработчик - СправочникСсылка.ОбменыДанных - элемент справочника "Обмены и обработчики данных". 
//
Процедура ДополнитьСписокПродуктивнымиБазами(Обработчик) Экспорт
	
	Если Не ЗначениеЗаполнено(Обработчик) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиИБИОбменов.БазаДанныхДляОбмена КАК БазаДанныхДляОбмена
		|ПОМЕСТИТЬ ТекущиеНастройки
		|ИЗ
		|	РегистрСведений.НастройкиИБИОбменов КАК НастройкиИБИОбменов
		|ГДЕ
		|	НастройкиИБИОбменов.Обмен = &Обмен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИнформационныеБазы.Ссылка КАК БазаДанныхДляОбмена,
		|	&Обмен КАК Обмен,
		|	Истина КАК Актуальность
		|ИЗ
		|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
		|ГДЕ
		|	НЕ ИнформационныеБазы.ПометкаУдаления
		|	И НЕ ИнформационныеБазы.Тестовая
		|	И ИнформационныеБазы.Среда = ЗНАЧЕНИЕ(Перечисление.Среда.Prod)
		|	И НЕ ИнформационныеБазы.Ссылка В
		|				(ВЫБРАТЬ
		|					ТекущиеНастройки.БазаДанныхДляОбмена КАК БазаДанныхДляОбмена
		|				ИЗ
		|					ТекущиеНастройки КАК ТекущиеНастройки)";
	
	Запрос.УстановитьПараметр("Обмен", Обработчик);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяНастройка = РегистрыСведений.НастройкиИБИОбменов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяНастройка, Выборка);
		НоваяНастройка.Записать();
	КонецЦикла;
	
КонецПроцедуры    

// Добавляет во все записи по обработчику параметры запроса   
//
// Параметры:
//  Обработчик - СправочникСсылка.ОбменыДанных - элемент справочника "Обмены и обработчики данных". 
//  Параметры - Структура - параметры запроса, сериализуемые в JSON. 
//
Процедура ДобавитьПараметрыЗапроса(Обработчик, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Обработчик) И Не (ТипЗнч(Параметры) = Тип("Структура") И Параметры.Количество()) Тогда
		Возврат;
	КонецЕсли; 
	
	ПараметрыЗапроса = ЗаписатьЗначениеJSON(Параметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиИБИОбменов.БазаДанныхДляОбмена КАК БазаДанныхДляОбмена
		|ИЗ
		|	РегистрСведений.НастройкиИБИОбменов КАК НастройкиИБИОбменов
		|ГДЕ
		|	НастройкиИБИОбменов.Обмен = &Обмен";
	
	Запрос.УстановитьПараметр("Обмен", Обработчик);
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл  
		
		Менеджер = СоздатьМенеджерЗаписи();	
		Менеджер.Обмен = Обработчик;
		Менеджер.БазаДанныхДляОбмена = Выборка.БазаДанныхДляОбмена;    
		Менеджер.Прочитать();
		Если Менеджер.Выбран() Тогда
			Менеджер.ПараметрыЗапроса = ПараметрыЗапроса;
			Менеджер.Записать();
		КонецЕсли;
		
	КонецЦикла;   
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#КонецЕсли
