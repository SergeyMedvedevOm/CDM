#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Перемещает сообщение из очереди на отправку в регистр истории отправки сообщений.
//
// Параметры:
//   Узел - ПланОбменаСсылка.ПередачаМастерданных - узел обмена.
//   МоментВремени - Число - момент времени.
//   Идентификатор - УникальныйИдентификатор - идентификатор сообщения.
//
Процедура ПеренестиСообщениеВИсторию(Узел, МоментВремени, Идентификатор) Экспорт
	
	МенеджерЗаписиОчередь = РегистрыСведений.СообщенияПередачиМастерданных.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОчередь.Узел = Узел;
	МенеджерЗаписиОчередь.МоментВремени = МоментВремени;
	МенеджерЗаписиОчередь.Идентификатор = Идентификатор;
	МенеджерЗаписиОчередь.Прочитать();
	
	Если Не МенеджерЗаписиОчередь.Выбран() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		МенеджерЗаписиИстория = РегистрыСведений.ИсторияПередачиМастерданных.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиИстория, МенеджерЗаписиОчередь);
		МенеджерЗаписиИстория.ДатаОтправки = ТекущаяДатаСеанса();
		МенеджерЗаписиИстория.Записать(Истина);
		
		МенеджерЗаписиОчередь.Удалить();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
