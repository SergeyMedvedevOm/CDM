#Область ОбработчикиСобытийФормы   

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастройкиАвторизации = ИнтеграцияКлиент.ПолучитьНастройкиАвторизации();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Тестовая", ОбщегоНазначенияCDMПовтИсп.ЭтоРабочаяБаза());
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА КАК Отметка,
		|	ИнформационныеБазы.Ссылка КАК ИБ,
		|	ИнформационныеБазы.ПутьНаВебСервере КАК ПутьКБазе,
		|	""hs/um"" КАК ПутьКСервису
		|ИЗ
		|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
		|ГДЕ
		|	НЕ ИнформационныеБазы.ПометкаУдаления
		|	И НЕ ИнформационныеБазы.Тестовая = &Тестовая";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Получатели.Добавить(), Выборка);
	КонецЦикла; 
	
	Если Параметры.Свойство("ОбъектМетаданных") Тогда
		ОбъектМетаданных = Параметры.ОбъектМетаданных;	 
		ОбъектМетаданныхПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеНастроекАвторизации" Тогда		
		НастройкиАвторизации = ИнтеграцияКлиент.ПолучитьНастройкиАвторизации();		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

 &НаКлиенте
Процедура ОбъектМетаданныхПриИзменении(Элемент)
	ОбъектМетаданныхПриИзмененииНаСервере();
КонецПроцедуры 

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьНаСервере(); 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)	
	Для Каждого Стр Из Получатели Цикл
		Стр.Отметка = Истина;	
	КонецЦикла; 
КонецПроцедуры 

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого Стр Из Получатели Цикл
		Стр.Отметка = Ложь;	
	КонецЦикла;
КонецПроцедуры

#КонецОбласти     

#Область СлужебныеПроцедурыИФункции   
 
&НаСервере
Процедура ОбъектМетаданныхПриИзмененииНаСервере()
	 
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	""/"" + (ВЫРАЗИТЬ(ОбъектыМетаданныхИнформационныхБаз.Владелец.Код КАК СТРОКА(50))) + ""/"" + (ВЫРАЗИТЬ(ОбъектыМетаданныхИнформационныхБаз.ПредставлениеВСервисе КАК СТРОКА(50))) КАК ПутьКОбъекту
		|ИЗ
		|	Справочник.ОбъектыМетаданныхИнформационныхБаз КАК ОбъектыМетаданныхИнформационныхБаз
		|ГДЕ
		|	ОбъектыМетаданныхИнформационныхБаз.Ссылка = &ОбъектМетаданных";
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Выборка.Следующий();
	
	Для Каждого Стр Из Получатели Цикл
		Стр.ПутьКОбъекту = Выборка.ПутьКОбъекту;	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьНаСервере()  
	
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбъектыМетаданныхИнформационныхБаз.Владелец.Код КАК ВладелецКод,
		|	ОбъектыМетаданныхИнформационныхБаз.Владелец.Наименование КАК ВладелецНаименование,
		|	ОбъектыМетаданныхИнформационныхБаз.ИмяОбъекта КАК ИмяОбъекта,
		|	ОбъектыМетаданныхИнформационныхБаз.ПредставлениеВСервисе КАК ПредставлениеВСервисе,
		|	ОбъектыМетаданныхИнформационныхБаз.ТекстЗапроса КАК ТекстЗапроса,
		|	ОбъектыМетаданныхИнформационныхБаз.КлючПоиска КАК КлючПоиска,
		|	ОбъектыМетаданныхИнформационныхБаз.ОбработчикПередЗаписью КАК ОбработчикПередЗаписью,
		|	ОбъектыМетаданныхИнформационныхБаз.ОбработчикПриЗаписи КАК ОбработчикПриЗаписи
		|ИЗ
		|	Справочник.ОбъектыМетаданныхИнформационныхБаз КАК ОбъектыМетаданныхИнформационныхБаз
		|ГДЕ
		|	ОбъектыМетаданныхИнформационныхБаз.Ссылка = &ОбъектМетаданных";
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Выборка.Следующий();
			
	Если НастройкиАвторизации.ИспользуетсяАутентификацияОС Тогда 
		Аутентификация = Новый Структура("Аутентификация", КоннекторHTTP.НоваяАутентификацияОС());		
	Иначе                                                    
		Аутентификация = Новый Структура("Аутентификация", Новый Структура("Пользователь, Пароль", НастройкиАвторизации.ИмяПользователя, НастройкиАвторизации.Пароль));	
	КонецЕсли;   	
	
	Для Каждого Стр Из Получатели Цикл  
		
		Если Не Стр.Отметка Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка			  		
			
			ВидОбъектаМетаданных = "";
			Если Выборка.ВладелецКод = "Catalogs" Или Выборка.ВладелецКод = "Documents" Тогда
				ВидОбъектаМетаданных = Лев(Выборка.ВладелецНаименование, СтрДлина(Выборка.ВладелецНаименование)-1);
			ИначеЕсли Выборка.ВладелецКод = "DataProcessings" Тогда
				ВидОбъектаМетаданных = "Обработка";
			Иначе
				ВидОбъектаМетаданных = СтрЗаменить(СтрЗаменить(Трег(Выборка.ВладелецНаименование), "ы", ""), " ", "");				
			КонецЕсли;
			
			ПараметрыРегистрации = Интеграция.НовыйПараметрыРегистрацииОбъектаМетаданных();
			ПараметрыРегистрации.fullname = ВидОбъектаМетаданных + "." + Выборка.ИмяОбъекта;
			ПараметрыРегистрации.parent = Выборка.ВладелецКод;
			ПараметрыРегистрации.name = Выборка.ПредставлениеВСервисе;
			ПараметрыРегистрации.request = Выборка.ТекстЗапроса;
			ПараметрыРегистрации.searchkey = Выборка.КлючПоиска;
			ПараметрыРегистрации.beforewright = Выборка.ОбработчикПередЗаписью;
			ПараметрыРегистрации.atwright = Выборка.ОбработчикПриЗаписи;
			
			Интеграция.ЗарегистрироватьОбъектМетаданных(Стр.ПутьКБазе, Аутентификация, ПараметрыРегистрации);
			
			Стр.Результат = "Ок";
					
		Исключение
			Стр.Результат = "Ошибка регистрации объекта метаданных по адресу " + Стр.ПутьКБазе + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()); 
		КонецПопытки;
		
	КонецЦикла;	

КонецПроцедуры  



#КонецОбласти

